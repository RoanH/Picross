plugins{
	id 'java'
	id 'application'
	id 'eclipse'
	id 'com.github.johnrengelman.shadow' version '5.1.0'
	id 'edu.sc.seis.launch4j' version '2.4.6'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['src']
		}
	}
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
version = 'v1.0'//Don't forget Main.java
shadowJar.archiveName = 'Picross-' + project.version + '.jar'

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			url "$nexusPublic"
		}
	}
	jcenter()
	maven{
		url "https://jitpack.io"
	}
}

dependencies{
	implementation 'com.github.RoanH:Util:v1.0'
}

application{
	mainClassName = 'me.roan.picross.Main'
}

launch4j{
	mainClassName = application.mainClassName
	icon = "${projectDir}/icon.ico"
	jreMinVersion = project.sourceCompatibility
	outfile = project.name + '-' + project.version + '.exe'
	fileDescription = project.name
	version = project.version.substring(1)
	textVersion = project.version.substring(1)
	copyright = "Roan Hofland"
}
